apply plugin: 'com.android.library'
apply plugin: 'com.google.protobuf'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.13'
    }
}

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation"  : "$projectDir/tests/unit/schemas".toString(),
                ]
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            proto {
                srcDir java.srcDirs
            }
        }

        test {
            java.srcDirs = ['tests/unit/src']
            assets.srcDirs += ['tests/unit/schemas']
        }
    }

    lintOptions {
        abortOnError false
    }

    testOptions {
        unitTests.includeAndroidResources = true
    }
}

dependencies {
    def lifecycle_version = "2.3.1"

    implementation "androidx.core:core:1.5.0-rc01"
    implementation "androidx.core:core-ktx:1.3.2"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-service:$lifecycle_version"
    implementation "androidx.annotation:annotation:1.2.0"
    implementation "com.google.guava:guava:30.0-android"
    implementation "com.google.protobuf:protobuf-java:3.10.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-guava:1.6.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation project(path: ":android-auto-companion-android:trustagent")

    testImplementation "androidx.arch.core:core-testing:2.1.0"
    testImplementation "androidx.test.ext:junit:1.1.2"
    testImplementation "androidx.test:core:1.3.0"
    testImplementation "androidx.test:rules:1.3.0"
    testImplementation "com.android.support.test:runner:1.0.2"
    testImplementation "com.android.support:support-annotations:28.0.0"
    testImplementation "com.google.dexmaker:dexmaker:1.2"
    testImplementation "com.google.truth:truth:1.1.2"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:1.4.32"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.1"
    testImplementation "org.mockito:mockito-core:3.6.0"
    testImplementation "org.robolectric:robolectric:4.5.1"

}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.10.0'
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}
